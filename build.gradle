buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
            jcenter()
        }
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:3.10.0'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.4.RELEASE'
    }
}

subprojects {
    repositories {
        mavenCentral()
    }
    apply plugin: 'java'
    apply plugin: 'com.diffplug.gradle.spotless'

    spotless {
        java {
            indentWithSpaces(4)
            removeUnusedImports()
        }
    }
}

configure([project(':hydra-client'), project(':hydra-common'), project(':hydra-gradle-core'), 
           project('hydra-gradle-plugin'), project('hydra-android-gradle-plugin')]) {
    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc
    }
}

configure (project(':hydra-server')) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'war'

    bootRun {
        // support passing -Dsystem.property=value to bootRun task
        systemProperties = System.properties
        systemProperties['log4j.configuration'] = "$rootDir/deployment/prod/log4j.properties"
        sourceResources sourceSets.main
    }
}

apply from: 'build.publication.gradle'